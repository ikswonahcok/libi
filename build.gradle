plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'antlr'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:${antlrVersion}"
    compileOnly "org.antlr:antlr4-runtime:${antlrVersion}"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.shell:spring-shell-starter:${springShellVersion}"
    implementation "org.jgrapht:jgrapht-core:${jgraphtVersion}"
    implementation "org.jgrapht:jgrapht-io:${jgraphtVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

generateGrammarSource {
    arguments += ['-package', 'org.github.libi.libiel', '-visitor', '-no-listener']
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

bootJar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
    }
}
